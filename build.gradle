plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'org.betterplugin.avro' version '0.19.2-SNAPSHOT'
    id 'com.palantir.docker'  version '0.25.0'
}

group = 'dev.flanker'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
}



dependencies {
    implementation 'org.springdoc:springdoc-openapi-ui:1.3.9'
    implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.3.9'

    implementation 'org.apache.avro:avro:1.9.2'
    implementation 'com.google.cloud:google-cloud-pubsub:1.105.1'
    implementation 'com.spotify:futures-extra:4.2.0'

    implementation 'io.grpc:grpc-all:1.29.0'

    implementation 'com.datastax.oss:java-driver-core:4.6.1'

    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-jetty'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation 'org.mockito:mockito-core:3.3.3'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}

avro {
    fieldVisibility = "PRIVATE"
    outputCharacterEncoding = "UTF-8"
    stringType = "String"
    dateTimeLogicalType = "JSR310"
}


def generateProtocol = tasks.register("generateProtocol", org.betterplugin.avro.GenerateAvroProtocolTask) {
    delete("./build/generated-avro-main-avpr")
    source file("./src/main/resources/avro")
    include("**/*.avdl")
    outputDir = file("./build/generated-avro-main-avpr")
}

def generateSchema = tasks.register("generateSchema", org.betterplugin.avro.GenerateAvroSchemaTask) {
    delete("./build/generated-main-avro-avsc")
    dependsOn generateProtocol
    source file("./build/generated-avro-main-avpr")
    include("**/*.avpr")
    outputDir = file("./build/generated-main-avro-avsc")
}

def generateAvro = tasks.register("generateAvro", org.betterplugin.avro.GenerateAvroJavaTask) {
    delete("./src/main/java/dev/flanker/cart/generated/avro")
    dependsOn generateSchema
    source("./build/generated-main-avro-avsc")
    outputDir = file("./src/main/java/")
}

tasks.named("compileJava").configure {
    source(generateAvro)
}

bootJar {
    baseName = 'cart-microservice'
    version =  '0.1.0'
}


task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name "${project.group}/${bootJar.baseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}
